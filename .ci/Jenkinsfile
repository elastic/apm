// Licensed to Elasticsearch B.V. under one or more contributor
// license agreements. See the NOTICE file distributed with
// this work for additional information regarding copyright
// ownership. Elasticsearch B.V. licenses this file to you under
// the Apache License, Version 2.0 (the "License"); you may
// not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

@Library('apm@current') _

pipeline {
  agent { label 'linux && immutable' }
  environment {
    REPO = 'apm'
    BASE_DIR = "src/github.com/elastic/${env.REPO}"
    HOME = "${env.WORKSPACE}"
    NOTIFY_TO = credentials('notify-to')
    JOB_GCS_BUCKET = credentials('gcs-bucket')
    JOB_GIT_CREDENTIALS = "f6c7695a-671e-4f4f-a331-acdce44ff9ba"
    PATH = "${env.PATH}:${env.WORKSPACE}/bin"
    PIPELINE_LOG_LEVEL='INFO'
  }
  options {
    timeout(time: 3, unit: 'HOURS')
    buildDiscarder(logRotator(numToKeepStr: '5', artifactNumToKeepStr: '5'))
    timestamps()
    ansiColor('xterm')
    disableResume()
    durabilityHint('PERFORMANCE_OPTIMIZED')
  }
  triggers {
    cron('H H(4-5) * * 1,5')
  }
  parameters {
    booleanParam(name: 'DRY_RUN_MODE', defaultValue: false, description: 'If true, allows to execute this pipeline in dry run mode, without sending a PR.')
    booleanParam(name: 'FORCE_SEND_PR', defaultValue: false, description: 'If true, will force sending a PR, although it could be affected by the value off the DRY_RUN parameter: if the latter is true, a message will be printed in the console.')
    choice(name: 'APM_AGENTS', choices: ['All','.NET', 'Go', 'Java', 'Node.js', 'PHP', 'Python', 'Ruby', 'RUM'], description: 'Name of the APM Agent you want to update its specs.')
  }
  stages {
    stage('Checkout'){
      steps {
        deleteDir()
        gitCheckout(basedir: "${BASE_DIR}", branch: 'master',
          repo: "git@github.com:elastic/${REPO}.git",
          credentialsId: "${JOB_GIT_CREDENTIALS}"
        )
        stash allowEmpty: true, name: 'source', useDefaultExcludes: false
      }
    }
    // This stage will populate the environment, and will only be executed under any of the
    // following conditions:
    // 1. we run the pipeline NOT in DRY_RUN_MODE, because we want to send real PRs
    // 2. we run the pipeline forcing sending real PRs, because we want so
    // Because the rest of the following stages will need these variables to check for changes,
    // skipping this stage would not take effect in them, as they are covered by the 
    // FORCE_SEND_PR check.
    stage('Check for spec changes'){
      when {
        beforeAgent true
        anyOf {
          expression { return env.DRY_RUN_MODE == "false" }
          expression { return params.FORCE_SEND_PR }
        }
      }
      steps {
        deleteDir()
        unstash 'source'
        script {
          dir("${BASE_DIR}"){
            def regexps =[
              "^tests/agents/gherkin-specs/.*"
            ]
            env.GHERKIN_SPECS_UPDATED = isGitRegionMatch(
              from: "${env.GIT_PREVIOUS_SUCCESSFUL_COMMIT}",
              patterns: regexps)

            // regexp for JSON specs
            regexps =[
              "^tests/agents/json-specs/.*"
            ]
            env.JSON_SPECS_UPDATED = isGitRegionMatch(
              from: "${env.GIT_PREVIOUS_SUCCESSFUL_COMMIT}",
              patterns: regexps)
            env.SPEC_UPDATED = env.JSON_SPECS_UPDATED.equals('true') || env.GHERKIN_SPECS_UPDATED.equals('true')
            env.PR_DESCRIPTION = createPRDescription()
          }
        }
      }
    }
    stage('Send Pull Request for BDD specs'){
      options {
        warnError('Pull Requests to APM agents failed')
      }
      when {
        beforeAgent true
        anyOf {
          expression { return env.GHERKIN_SPECS_UPDATED == "true" }
          expression { return params.FORCE_SEND_PR }
        }
      }
      environment {
        // agentMapping is defined in the shared library as a map
        SELECTED_AGENT = agentMapping.id(params.APM_AGENTS)
      }
      steps {
        deleteDir()
        unstash 'source'
        dir("${BASE_DIR}"){
          script {
            def agents = readYaml(file: '.ci/.jenkins-agents.yml')
            def parallelTasks = [:]
            agents['agents'].each { agent ->
              def repository = 'apm-agent-' + agent.REPO
              if (env.SELECTED_AGENT == 'All' || env.SELECTED_AGENT == repository) {
                parallelTasks["${agent.REPO}"] = generateStepForAgent(repo: "${agent.REPO}", filesType: "gherkin", filesPath: "${agent.FEATURES_PATH}")
              }
            }

            parallel(parallelTasks)
          }
        }
      }
    }
    stage('Send Pull Request for JSON specs'){
      options {
        warnError('Pull Requests to APM agents failed')
      }
      environment {
        // agentMapping is defined in the shared library as a map
        SELECTED_AGENT = agentMapping.id(params.APM_AGENTS)
      }
      when {
        beforeAgent true
        anyOf {
          expression { return env.JSON_SPECS_UPDATED == "true" }
          expression { return params.FORCE_SEND_PR }
        }
      }
      steps {
        deleteDir()
        unstash 'source'
        dir("${BASE_DIR}"){
          script {
            def agents = readYaml(file: '.ci/.jenkins-agents.yml')
            def parallelTasks = [:]
            agents['agents'].each { agent ->
              if (agent.JSON_SPECS_PATH) {
                def repository = 'apm-agent-' + agent.REPO
                if (env.SELECTED_AGENT == 'All' || env.SELECTED_AGENT == repository) {
                  parallelTasks["${agent.REPO}"] = generateStepForAgent(repo: "${agent.REPO}", filesType: "json", filesPath: "${agent.JSON_SPECS_PATH}")
                }
              }
            }

            parallel(parallelTasks)
          }
        }
      }
    }
  }
  post {
    cleanup {
      notifyBuildResult()
    }
  }
}

def generateStepForAgent(Map args = [:]){
  def repo = args.containsKey('repo') ? args.get('repo') : error('generateStepForAgent: repo argument is required')
  def filesType = args.containsKey('filesType') ? args.get('filesType') : error('generateStepForAgent: filesType argument is required. Valid values: [gherkin | json]')
  def filesPath = args.containsKey('filesPath') ? args.get('filesPath') : error('generateStepForAgent: filesPath argument is required')
  return {
    node('linux && immutable') {
      catchError(buildResult: 'SUCCESS', stageResult: 'UNSTABLE') {
        deleteDir()
        unstash 'source'
        dir("${BASE_DIR}"){
          setupAPMGitEmail(global: true)
          sh script: """.ci/scripts/prepare-spec-changes.sh "${repo}" "${filesType}" "${filesPath}" """, label: "Prepare ${filesType} changes for ${repo}"
          dir(".ci/${repo}") {
            if (params.DRY_RUN_MODE || isPR()) {
              echo "DRY-RUN: ${repo} with description: '${env.PR_DESCRIPTION}'"
            } else {
              githubCreatePullRequest(title: "test: synchronizing ${filesType} spec", labels: 'automation', description: "${env.PR_DESCRIPTION}")
            }
          }
        }
      }
    }
  }
}

def createPRDescription() {
  def message = """
  ### What
  APM agent specs automatic sync

  ### Why
  """
  if (params.FORCE_SEND_PR) {
    message += "*Manually forced with the CI automation job.*"
  }
  if (env?.SPEC_UPDATED?.equals('true')){
    def gitLog = sh(script: """
      git log --pretty=format:'* https://github.com/${env.ORG_NAME}/${env.REPO_NAME}/commit/%h %s' \
          ${env.GIT_PREVIOUS_SUCCESSFUL_COMMIT}...HEAD \
          --follow -- tests/agents \
      | sed 's/#\\([0-9]\\+\\)/https:\\/\\/github.com\\/${env.ORG_NAME}\\/${env.REPO_NAME}\\/pull\\/\\1/g' || true""", returnStdout: true)
    message += "*Changeset*\n${gitLog}"
  }
  return message
}
